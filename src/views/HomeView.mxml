<?xml version="1.0" encoding="utf-8"?>
<!--
Copyright (C) 2016  Johan Degraeve

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/gpl.txt>.

-->
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009" 
		xmlns:s="library://ns.adobe.com/flex/spark" title=""
		initialize="initializeHandler(event)"
		creationComplete="onCreationComplete(event)">
	
	<s:states>
		<s:State name="bluetoothred"/>
		<s:State name="bluetoothgreen"/>
		<s:State name="bluetoothorange"/>
	</s:states>
	<fx:Script>
		<![CDATA[
			import com.distriqt.extension.bluetoothle.BluetoothLE;
			import com.distriqt.extension.bluetoothle.BluetoothLEState;
			import com.distriqt.extension.bluetoothle.events.BluetoothLEEvent;
			import com.distriqt.extension.bluetoothle.events.PeripheralEvent;
			import com.distriqt.extension.dialog.DateTimeDialogView;
			import com.distriqt.extension.dialog.Dialog;
			import com.distriqt.extension.dialog.DialogType;
			import com.distriqt.extension.dialog.DialogView;
			import com.distriqt.extension.dialog.builders.ActionSheetBuilder;
			import com.distriqt.extension.dialog.builders.AlertBuilder;
			import com.distriqt.extension.dialog.builders.DateTimeDialogBuilder;
			import com.distriqt.extension.dialog.events.DialogDateTimeEvent;
			import com.distriqt.extension.dialog.events.DialogViewEvent;
			import com.distriqt.extension.dialog.objects.DialogAction;
			
			import mx.events.FlexEvent;
			
			import spark.formatters.DateTimeFormatter;
			
			import Utilities.BgGraphBuilder;
			
			import databaseclasses.BgReading;
			import databaseclasses.BlueToothDevice;
			import databaseclasses.Calibration;
			import databaseclasses.CommonSettings;
			import databaseclasses.Database;
			import databaseclasses.Sensor;
			
			import events.BlueToothServiceEvent;
			import events.CalibrationServiceEvent;
			import events.DatabaseEvent;
			import events.DialogServiceEvent;
			import events.NotificationServiceEvent;
			import events.TimerServiceEvent;
			import events.TransmitterServiceEvent;
			
			import model.ModelLocator;
			
			import services.BluetoothService;
			import services.CalibrationService;
			import services.DialogService;
			import services.NotificationService;
			import services.TimerService;
			import services.TransmitterService;
			
			private static var initialStart:Boolean = true;
			[Bindable]
			private static var currentBgLabelText:String = "---";
			[Bindable]
			private static var _calibrateButtonActive:Boolean = false;
			private static const ACTION_SHEET_INDEX_FOR_SCAN_FOR_DEVICE:int = 0;
			private static const ACTION_SHEET_INDEX_FOR_FORGET_DEVICE:int = 1;
			private static const ACTION_SHEET_INDEX_FOR_START_SENSOR:int = 2;
			private static const ACTION_SHEET_INDEX_FOR_STOP_SENSOR:int = 3;
			private static const ACTION_SHEET_INDEX_FOR_STATUS:int = 4;
			private static const ACTION_SHEET_INDEX_FOR_CANCEL:int = 6;
			
			//local time, ie if it's here and now 12:20 the 2nd of April 2017, then Hours = 12, ...
			private static var sensorStartSeconds:int = 0;
			private static var sensorStartMinutes:int = 0;
			private static var sensorStartHours:int = 0;
			private static var sensorStartDate:int = 0;
			private static var sensorStartYear:int = 0;
			private static var sensorStartMonth:int = 0;//starts at 0 for January
			
			/**
			 * connected or not. Should be replaced by if (BluetoothService.getActiveBluetoothPeripheral().state == PeripheralState.CONNECTED)<br>
			 * but this last one is not working see https://github.com/distriqt/ANE-BluetoothLE/issues/8
			 */
			private static var peripheralConnected:Boolean = false;
			/**
			 * timestamp that has changed connection status<br> 
			 */
			private static var peripheralConnectionStatusChangeTimestamp:Number = (new Date()).valueOf();
			
			[ResourceBundle("homeview")]
			[ResourceBundle("general")]
			[ResourceBundle("bluetoothservice")]
			
			[Bindable]
			private static var currentBgLabellinethrough:Boolean = false;
			
			protected function onCreationComplete(event:FlexEvent):void
			{
				if (initialStart) {
					Database.instance.addEventListener(DatabaseEvent.DATABASE_INIT_FINISHED_EVENT,onInitResult);
					Database.instance.addEventListener(DatabaseEvent.ERROR_EVENT,onInitError);
					Database.instance.addEventListener(DatabaseEvent.DATABASE_INFORMATION_EVENT, databaseInformationEventReceived);
					//need to know when modellocator is populated, then we can also update display
					Database.instance.addEventListener(DatabaseEvent.BGREADING_RETRIEVAL_EVENT, bgReadingReceivedFromDatabase);
					TimerService.instance.addEventListener(TimerServiceEvent.BG_READING_NOT_RECEIVED_ON_TIME, bgReadingNotReceivedOnTime);
					Database.init();
					initialStart = false;
				} else {
					//force now a check if the icon needs tobe changed by calling bluetoothStateChangedHandler, even though it may not have changed
					bluetoothStateChangedHandler(null);
					displayCurrentInfoFromReading(BgReading.lastNoSensor());
				}
				
				function bgReadingNotReceivedOnTime(event:TimerServiceEvent):void {
					displayCurrentInfoFromReading(BgReading.lastNoSensor());
					NotificationService.updateAllNotifications(null);
					if (!peripheralConnected) {
						if ((new Date().valueOf() - peripheralConnectionStatusChangeTimestamp) > TimerService.DELAY_FOR_CHECKING_BGREADING_IN_SECONDS) {
							currentState = "bluetoothred"; 
						}
					}
				}
				
				function bgReadingReceivedFromDatabase(de:DatabaseEvent):void {
					if (de.data != null)
						if (de.data is String) {
							if (de.data as String == Database.END_OF_RESULT) {
								Database.instance.removeEventListener(DatabaseEvent.BGREADING_RETRIEVAL_EVENT, bgReadingReceivedFromDatabase);
								displayCurrentInfoFromReading(BgReading.lastNoSensor());
							}
						}
				} 
				
				function onInitResult(event:Event):void
				{
					trace("HomeView : database init result ok");
					Database.instance.removeEventListener(DatabaseEvent.ERROR_EVENT, onInitError);
					Database.instance.removeEventListener(DatabaseEvent.DATABASE_INIT_FINISHED_EVENT, onInitResult);
					//at this moment the database is intialised, but the logs, bgreadings, ... might still be read in the ModelLocator, Modellocator is listening to the same event
					
					//set calibration button in the correct state
					if (Calibration.allForSensor().length > 1) {
						_calibrateButtonActive = true;
					}
					
					BluetoothService.instance.addEventListener(BlueToothServiceEvent.BLUETOOTH_SERVICE_INFORMATION_EVENT, blueToothServiceInformationReceived);
					BluetoothService.instance.addEventListener(BlueToothServiceEvent.BLUETOOTH_SERVICE_INITIATED, blueToothServiceInitiated);
					
					TransmitterService.instance.addEventListener(TransmitterServiceEvent.BGREADING_EVENT, transmitterServiceBGReadingEventReceived);
					
					CalibrationService.instance.addEventListener(CalibrationServiceEvent.INITIAL_CALIBRATION_EVENT, initialCalibrationEventReceived);
					CalibrationService.instance.addEventListener(CalibrationServiceEvent.NEW_CALIBRATION_EVENT, newCalibrationEventReceived);
				}
				
				function onInitError(event:Event):void
				{	
					Database.instance.removeEventListener(DatabaseEvent.ERROR_EVENT, onInitError);
					Database.instance.removeEventListener(DatabaseEvent.DATABASE_INIT_FINISHED_EVENT, onInitResult);
					trace("HomeView : database init error");
				}
			}
			
			public static function notificationServiceInitiated(ev:NotificationServiceEvent):void {
				NotificationService.instance.removeEventListener(NotificationServiceEvent.NOTIFICATION_SERVICE_INITIATED_EVENT, HomeView.notificationServiceInitiated);
				NotificationService.updateAllNotifications(null);

				if (DialogService.isInitiated) {
					dialogServiceInitiated(null);
				} else {
					DialogService.instance.addEventListener(DialogServiceEvent.DIALOG_SERVICE_INITIATED_EVENT, dialogServiceInitiated);
				}
			}
			
			private static function dialogServiceInitiated(ev:DialogServiceEvent):void {
				DialogService.instance.removeEventListener(DialogServiceEvent.DIALOG_SERVICE_INITIATED_EVENT, dialogServiceInitiated);
					
				//initial info screen
				//this seems to be the right time to start using the database - here it is used actually
				if (CommonSettings.getCommonSetting(CommonSettings.COMMON_SETTING_INITIAL_INFO_SCREEN_1_SHOWN_ID) == "false") {
					var alert:DialogView = Dialog.service.create(
						new AlertBuilder()
						.setTitle(ModelLocator.resourceManagerInstance.getString("homeview","info"))
						.setMessage(ModelLocator.resourceManagerInstance.getString("homeview","info_screen_1"))//info about buttons top right corner
						.addOption("Ok", DialogAction.STYLE_POSITIVE, 0)
						.build()
					);
					DialogService.addDialog(alert);
					CommonSettings.setCommonSetting(CommonSettings.COMMON_SETTING_INITIAL_INFO_SCREEN_1_SHOWN_ID,"true");
				}
			}
			
			
			private function central_peripheralConnectHandler(event:PeripheralEvent):void {
				currentState = "bluetoothgreen";
				peripheralConnected = true;
				peripheralConnectionStatusChangeTimestamp = (new Date()).valueOf();
			}
			
			private function userInitiatedScanningSucceeded(event:PeripheralEvent):void {
				BluetoothLE.service.centralManager.removeEventListener(PeripheralEvent.CONNECT, userInitiatedScanningSucceeded);
				var alert:DialogView = Dialog.service.create(
					new AlertBuilder()
					.setTitle(ModelLocator.resourceManagerInstance.getString("homeview","scan_for_device"))
					.setMessage(ModelLocator.resourceManagerInstance.getString("homeview","connected_to_peripheral_device_id_stored"))
					.addOption("Ok", DialogAction.STYLE_POSITIVE, 0)
					
					.build()
				);
				DialogService.addDialog(alert, 30);
			}
			
			private function central_peripheralDisconnectHandler(event:PeripheralEvent):void {
				peripheralConnected = false;
				peripheralConnectionStatusChangeTimestamp = (new Date()).valueOf();
				if ((BluetoothLE.service.centralManager.state == BluetoothLEState.STATE_ON) && BlueToothDevice.known()) {
					currentState = "bluetoothorange";
				} else {
					currentState = "bluetoothred";
				}
			}
			
			private function bluetoothStateChangedHandler(event:BluetoothLEEvent):void {
				if (BluetoothLE.service.centralManager.state == BluetoothLEState.STATE_ON) {
					if (BluetoothService.bluetoothPeripheralActive()) {
						if (peripheralConnected) {
							//peripheral is known in bluetoothservice and it is connected
							currentState = "bluetoothgreen";
						} else {
							//peripheral is known, but not active, orange
							currentState = "bluetoothorange";
						}
					} else {//bluetoothperipheral not known in bluetoothservice, but maybe a device name is known in bluetoothdevice
						if (BlueToothDevice.known()) {
							//we know a device, we just didn't scan it yet, good for orange
							currentState = "bluetoothorange";
						} else {
							//we don't know a device yet, scanning for new device still needs to happen, that's red 
							currentState = "bluetoothred";
						}
					}
				} else {
					//definitely a case for red, because bluetooth is not on
					currentState = "bluetoothred";
				}
				
			}
			
			private function blueToothServiceInformationReceived(be:BlueToothServiceEvent):void {
				trace((new Date()).toTimeString() + " : HomeView : received bluetooth service information = " + be.data.information);
			}
			
			private function blueToothServiceInitiated(be:BlueToothServiceEvent):void {
				BluetoothService.instance.removeEventListener(BlueToothServiceEvent.BLUETOOTH_SERVICE_INITIATED, blueToothServiceInitiated);
				BluetoothLE.service.centralManager.addEventListener(PeripheralEvent.CONNECT, central_peripheralConnectHandler);
				BluetoothLE.service.centralManager.addEventListener(PeripheralEvent.DISCONNECT, central_peripheralDisconnectHandler);
				BluetoothLE.service.addEventListener(BluetoothLEEvent.STATE_CHANGED, bluetoothStateChangedHandler);
				//bluetooth service is initiated
				//force now a check if the icon needs tobe changed by calling bluetoothStateChangedHandler, even though it may not have changed
				bluetoothStateChangedHandler(null);
			}
			
			private function transmitterServiceBGReadingEventReceived(be:TransmitterServiceEvent):void {
				displayCurrentInfoFromReading(BgReading.lastNoSensor());
			}
			
			private function initialCalibrationEventReceived(be:CalibrationServiceEvent):void {
				displayCurrentInfoFromReading(BgReading.lastNoSensor());
				_calibrateButtonActive = true;
			}
			
			private function newCalibrationEventReceived(be:CalibrationServiceEvent):void {
				displayCurrentInfoFromReading(BgReading.lastNoSensor());
			}
			
			private function databaseInformationEventReceived(be:DatabaseEvent):void {
				//trace((new Date()).toTimeString() + " : HomeView : received database  information = " + be.data.information);
			}
			
			protected function initializeHandler(event:FlexEvent):void
			{
				//instantiate modellocator because event listeners are being added to the database class, to receive logging information
				ModelLocator.instance;
			}
			
			private function displayCurrentInfoFromReading(lastBgReading:BgReading):void {
				var estimate:Number = new Number(0);
				if (lastBgReading == null) {
					currentBgLabelText = "---";
					currentBgLabellinethrough = false;//currentBgLabel.setStyle("lineThrough","false");
					currentBgLabel.invalidateSize();
				} else {
					if ((new Date().getTime()) - (60000 * 11) - lastBgReading.timestamp > 0) {
						//notificationText = ModelLocator.resourceManagerInstance.getString('homeview','signalmissed');
						estimate = lastBgReading.calculatedValue;
						currentBgLabelText = BgGraphBuilder.unitizedString(estimate, true);
						currentBgLabellinethrough = true;//currentBgLabel.setStyle("lineThrough","true");
						currentBgLabel.invalidateSize();
					} else {
						/*if (notificationText.getText().length()==0){
						notificationText.setTextColor(Color.WHITE);
						}*/
						estimate = lastBgReading.calculatedValue;
						currentBgLabelText = BgGraphBuilder.unitizedString(estimate, true);
						var slope_arrow:String = lastBgReading.slopeArrow();
						if (lastBgReading.hideSlope) {
							slope_arrow = "";
						}
						currentBgLabelText += " " + slope_arrow;
						currentBgLabellinethrough = false;//currentBgLabel.setStyle("lineThrough","false");
						currentBgLabel.invalidateSize();
					}
					var minutes:Number = ((new Date()).valueOf() - lastBgReading.timestamp) / (60 * 1000);
					var minutesString:String = minutes==1 ?" Minute ago":" Minutes ago";
					
					/// TODO complete this as in Android version
					//notificationText.append("\n" + minutes + minutesString);
					/*List<BgReading> bgReadingList = BgReading.latest(2);
					if(bgReadingList != null && bgReadingList.size() == 2) {
					// same logic as in xDripWidget (refactor that to BGReadings to avoid redundancy / later inconsistencies)?
					if(BgGraphBuilder.isXLargeTablet(getApplicationContext()) || BgGraphBuilder.isLargeTablet(getApplicationContext())) {
					notificationText.append("  ");
					} else {
					notificationText.append("\n");
					}
					notificationText.append(
					bgGraphBuilder.unitizedDeltaString(true, true));
					}
					if(bgGraphBuilder.unitized(estimate) <= bgGraphBuilder.lowMark) {
					currentBgLabelText.setTextColor(Color.parseColor("#FF0000"));
					} else if (bgGraphBuilder.unitized(estimate) >= bgGraphBuilder.highMark) {
					currentBgLabelText.setTextColor(Color.parseColor("#FFFF00"));
					} else {
					currentBgLabelText.setTextColor(Color.WHITE);
					}*/
				}
			}
			
			private function calibrationButtonClicked(e:MouseEvent = null):void {
				if (!_calibrateButtonActive)
					return;
				CalibrationService.calibrationOnRequest();
			}
			
			private function bluetoothButtonClicked(e:MouseEvent = null):void {
				var actionsheetBuilder:ActionSheetBuilder = new ActionSheetBuilder();;
				
				actionsheetBuilder.addAction(ModelLocator.resourceManagerInstance.getString('homeview','view_device_status'), DialogAction.STYLE_DEFAULT, ACTION_SHEET_INDEX_FOR_STATUS);
				
				if (!BluetoothService.bluetoothPeripheralActive()) {
					//this is the case where bluetoothdevice is known or not, time for a scan
					actionsheetBuilder.addAction(ModelLocator.resourceManagerInstance.getString('homeview','scan_for_device'), DialogAction.STYLE_DEFAULT, ACTION_SHEET_INDEX_FOR_SCAN_FOR_DEVICE);
				} 
				
				if (BlueToothDevice.known()) {
					actionsheetBuilder.addAction(ModelLocator.resourceManagerInstance.getString('homeview','forget_device'), DialogAction.STYLE_DEFAULT, ACTION_SHEET_INDEX_FOR_FORGET_DEVICE);
				}
				
				if (Sensor.getActiveSensor() == null) {
					actionsheetBuilder.addAction(ModelLocator.resourceManagerInstance.getString('homeview','start_sensor'), DialogAction.STYLE_DEFAULT, ACTION_SHEET_INDEX_FOR_START_SENSOR);
				} else {
					actionsheetBuilder.addAction(ModelLocator.resourceManagerInstance.getString('homeview','stop_sensor'), DialogAction.STYLE_DEFAULT, ACTION_SHEET_INDEX_FOR_STOP_SENSOR);
				}
				
				actionsheetBuilder.addAction(ModelLocator.resourceManagerInstance.getString('general','cancel'),DialogAction.STYLE_CANCEL, ACTION_SHEET_INDEX_FOR_CANCEL);
				
				var actionSheet:DialogView = Dialog.service.create(actionsheetBuilder.build());
				actionSheet.addEventListener(DialogViewEvent.CLOSED, actionSheet_closedHandler);
				actionSheet.show();
			}
			
			private function actionSheet_closedHandler( event:DialogViewEvent ):void {
				var actionSheet:DialogView = DialogView(event.currentTarget);
				actionSheet.removeEventListener(DialogViewEvent.CLOSED, actionSheet_closedHandler);
				actionSheet.dispose();
				
				if (event.index == ACTION_SHEET_INDEX_FOR_SCAN_FOR_DEVICE) {
					//TODO add a check if bluetooth is on, if not give warning message and don't start scanning
					if (BluetoothLE.service.centralManager.state == BluetoothLEState.STATE_ON) {
						BluetoothService.instance.addEventListener(BlueToothServiceEvent.STOPPED_SCANNING, scanningStopped);
						BluetoothLE.service.centralManager.addEventListener(PeripheralEvent.CONNECT, userInitiatedScanningSucceeded);
						BluetoothService.startScanning();
					} else {
						var alert:DialogView = Dialog.service.create(
							new AlertBuilder()
							.setTitle(ModelLocator.resourceManagerInstance.getString("homeview","scanning_failed_tile"))
							.setMessage(ModelLocator.resourceManagerInstance.getString("homeview","bluetooth_not_switched_on"))
							.addOption("Ok", DialogAction.STYLE_POSITIVE, 0)
							.build()
						);
						DialogService.addDialog(alert, 30);
					}
				} else if (event.index == ACTION_SHEET_INDEX_FOR_FORGET_DEVICE) {
					BlueToothDevice.forgetBlueToothDevice();
					currentState = "bluetoothred";
					
				} else if (event.index == ACTION_SHEET_INDEX_FOR_START_SENSOR) {
					var alert:DialogView = Dialog.service.create(
						new AlertBuilder()
						.setTitle(ModelLocator.resourceManagerInstance.getString("homeview","start_sensor"))
						.setMessage(ModelLocator.resourceManagerInstance.getString("homeview","start_sensor_warning_correct_timing"))
						.addOption("Ok", DialogAction.STYLE_POSITIVE, 0)
						.addOption(ModelLocator.resourceManagerInstance.getString("general","cancel"), DialogAction.STYLE_CANCEL, 1)
						.build()
					);
					alert.addEventListener(DialogViewEvent.CLOSED, startSensorWarningConfirmed);
					alert.addEventListener(DialogViewEvent.CANCELLED, startSensorWarningCancelled);
					DialogService.addDialog(alert, 60);
				} else if (event.index == ACTION_SHEET_INDEX_FOR_STOP_SENSOR) {
					var alert:DialogView = Dialog.service.create(
						new AlertBuilder()
						.setTitle(ModelLocator.resourceManagerInstance.getString("homeview","stop_sensor"))
						.setMessage(ModelLocator.resourceManagerInstance.getString("homeview","stop_sensor_ask_confirmation"))
						.addOption("Ok", DialogAction.STYLE_POSITIVE, 0)
						.addOption(ModelLocator.resourceManagerInstance.getString("general","cancel"), DialogAction.STYLE_CANCEL, 1)
						.build()
					);
					alert.addEventListener(DialogViewEvent.CLOSED, stopSensorConfirmation);
					alert.addEventListener(DialogViewEvent.CANCELLED, stopSensorcancellation);
					DialogService.addDialog(alert, 60);
				} 
			}
			
			private function startSensorWarningCancelled(event:DialogViewEvent):void {
			}
			
			private function startSensorWarningConfirmed(event:DialogViewEvent):void {
				if (event.index == 1) {
					return;
				}
				sensorStartYear = 0; sensorStartMonth = 0; sensorStartDate = 0; sensorStartHours = 0; sensorStartMinutes = 0; sensorStartSeconds = 0;
				var dateTime:DateTimeDialogView = Dialog.service.create(
					new DateTimeDialogBuilder()
					.setMode(DialogType.MODE_DATE)
					.setTitle(ModelLocator.resourceManagerInstance.getString("homeview","start_sensor_title_get_date"))
					.setAcceptLabel("Ok")
					.setCancelable(true)
					.setCancelLabel(ModelLocator.resourceManagerInstance.getString('general','cancel'))
					.build()
				);
				dateTime.addEventListener(DialogViewEvent.CLOSED, date_closedRequestHandler);
				dateTime.addEventListener(DialogDateTimeEvent.SELECTED, date_selectedHandler);
				//dateTime.addEventListener(DialogDateTimeEvent.CHANGED, dateTime_changedHandler);Not needed, this is triggered whenever the user clicks another date, but hasn't closed
				DialogService.addDialog(dateTime);
			}
			
			private static function stopSensorcancellation(event:DialogViewEvent):void {
			}
			
			private static function stopSensorConfirmation(event:DialogViewEvent):void {
				if (event.index == 1) {
					return;
				}
				Sensor.stopSensor();
				NotificationService.updateAllNotifications(null);
				_calibrateButtonActive = false;
			}
			
			private function date_selectedHandler( event:DialogDateTimeEvent ):void
			{
				sensorStartDate = event.date.date;
				sensorStartMonth = event.date.month;
				sensorStartYear = event.date.fullYear;
			}
			
			private function date_closedRequestHandler( event:DialogViewEvent ):void
			{
				if (sensorStartYear == 0) {
					//use pressed cancel
					return;
				}
				var dateTime:DateTimeDialogView = DateTimeDialogView(event.currentTarget);
				dateTime.removeEventListener(DialogViewEvent.CLOSED, date_closedRequestHandler);
				dateTime.removeEventListener(DialogDateTimeEvent.SELECTED, date_selectedHandler);
				dateTime.dispose();
				dateTime = Dialog.service.create(
					new DateTimeDialogBuilder()
					.setMode(DialogType.MODE_TIME)
					.setTitle(ModelLocator.resourceManagerInstance.getString("homeview","start_sensor_title_get_time"))
					.setAcceptLabel("Ok")
					.setCancelable(true)
					.setCancelLabel(ModelLocator.resourceManagerInstance.getString('general','cancel'))
					.build()
				);
				dateTime.addEventListener(DialogViewEvent.CLOSED, time_closedRequestHandler);
				dateTime.addEventListener(DialogDateTimeEvent.SELECTED, time_selectedHandler);
				//dateTime.addEventListener(DialogDateTimeEvent.CHANGED, dateTime_changedHandler);Not needed, this is triggered whenever the user clicks another date, but hasn't closed
				DialogService.addDialog(dateTime);
			}
			
			private function time_selectedHandler( event:DialogDateTimeEvent ):void
			{
				sensorStartHours = event.date.hours;
				sensorStartMinutes = event.date.minutes;
				sensorStartSeconds = event.date.seconds;
			}
			
			private function time_closedRequestHandler( event:DialogViewEvent ):void {
				if (sensorStartHours == 0) {
					//use pressed cancel
					return;
				}
				
				var dateTime:DateTimeDialogView = DateTimeDialogView(event.currentTarget);
				dateTime.removeEventListener(DialogViewEvent.CLOSED, date_closedRequestHandler);
				dateTime.removeEventListener(DialogDateTimeEvent.SELECTED, date_selectedHandler);
				dateTime.dispose();
				
				//ready to start the sensor
				var sensorStartTimeAsDate:Date = new Date(sensorStartYear, sensorStartMonth, sensorStartDate, sensorStartHours, sensorStartMinutes, sensorStartSeconds);
				var sensorStartTime:Number = sensorStartTimeAsDate.valueOf();
				if (sensorStartTime > (new Date()).valueOf() + 5 * 60 * 1000) {
					var alert:DialogView = Dialog.service.create(
						new AlertBuilder()
						.setTitle(ModelLocator.resourceManagerInstance.getString("homeview","start_sensor"))
						.setMessage(ModelLocator.resourceManagerInstance.getString("homeview","dont_start_with_time_in_future"))
						.addOption("Ok", DialogAction.STYLE_POSITIVE, 0)
						.build()
					);
					DialogService.addDialog(alert, 60);
				} else {
					Sensor.startSensor(sensorStartTime);
					var actualTime:Number = (new Date()).valueOf();
					var timeOfCalibration:Number = 2 * 3600 * 1000 - (actualTime - sensorStartTime);
					var dateFormatter:DateTimeFormatter = new DateTimeFormatter();
					dateFormatter.dateTimePattern = ModelLocator.resourceManagerInstance.getString('homeview','timestamppattern_for_sensor_start_warning');
					dateFormatter.useUTC = false;
					dateFormatter.setStyle("locale",Capabilities.language.substr(0,2));
					var message:String;
					if (timeOfCalibration > 0) {
						message = ModelLocator.resourceManagerInstance.getString("homeview","sensor_started_wait_needed_1");
						message += " " + dateFormatter.format(new Date(actualTime + timeOfCalibration)) + " ";
						message += ModelLocator.resourceManagerInstance.getString("homeview","sensor_started_wait_needed_2");
					} else {
						message = ModelLocator.resourceManagerInstance.getString("homeview","sensor_started_no_wait_needed");
					}
					var alert:DialogView = Dialog.service.create(
						new AlertBuilder()
						.setTitle(ModelLocator.resourceManagerInstance.getString("homeview","start_sensor"))
						.setMessage(message)
						.addOption("Ok", DialogAction.STYLE_POSITIVE, 0)
						.build()
					);
					DialogService.addDialog(alert, 60);
				}
			}
			
			private static function scanningStopped(event:Event):void {
				BluetoothService.instance.removeEventListener(BlueToothServiceEvent.STOPPED_SCANNING, scanningStopped);
				if (!BluetoothService.bluetoothPeripheralActive()) {
					//create alert to get the user's input
					var alert:DialogView = Dialog.service.create(
						new AlertBuilder()
						.setTitle(ModelLocator.resourceManagerInstance.getString("homeview","scanning_failed_tile"))
						.setMessage(ModelLocator.resourceManagerInstance.getString("homeview","scanning_failed")
							+
							(BlueToothDevice.known() ? (" " + ModelLocator.resourceManagerInstance.getString("homeview","with_name") + " " + BlueToothDevice.name) + 
								"\n\n" + ModelLocator.resourceManagerInstance.getString("homeview","explain_expected_device_name"): "")
						)
						.addOption("Ok", DialogAction.STYLE_POSITIVE, 0)
						.build()
					);
					DialogService.addDialog(alert, 30);
				}
			}
			
			
		]]>
	</fx:Script>
	<s:actionContent>
		<s:Button icon.bluetoothred="{ModelLocator.image_bluetooth_red}" icon.bluetoothgreen="{ModelLocator.image_bluetooth_green}" icon.bluetoothorange="{ModelLocator.image_bluetooth_orange}" click="bluetoothButtonClicked(event)"/>
		<s:Button icon="{ModelLocator.image_calibrate_active}" id="calibrateButton" alpha="{_calibrateButtonActive ? 1:0.25}" enabled="{_calibrateButtonActive}" click="calibrationButtonClicked(event)"/>
	</s:actionContent>
	
	<s:Group y="0" x="0" width="100%" height="100%" left="5" top="5" right="5">
		<s:layout>
			<s:VerticalLayout/>
		</s:layout>
		<s:Group width="100%">
			<s:layout>
				<s:HorizontalLayout paddingTop="5" paddingBottom="5" paddingLeft="5" paddingRight="5" gap="5" 
									horizontalAlign="right" verticalAlign="middle"/>
			</s:layout>
			<s:Group width="100%">
				<s:layout>
					<s:HorizontalLayout paddingTop="0" paddingBottom="0" paddingLeft="0" paddingRight="0" gap="0" 
										horizontalAlign="left" verticalAlign="middle"/>
				</s:layout>
				<s:Group>
					<s:layout>
						<s:VerticalLayout>
							
						</s:VerticalLayout>
					</s:layout>
					<s:Label id = "notificationText" text="test1"/>
					<s:Label id = "notificationText2" text="test2"/>
				</s:Group>
			</s:Group>
			<s:Label id = "currentBgLabel" text="{currentBgLabelText}" fontSize="70" lineThrough="{currentBgLabellinethrough}"/>
		</s:Group>
	</s:Group>
</s:View>
